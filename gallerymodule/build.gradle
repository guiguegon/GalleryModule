apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def versionLib = '1.0.2'

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

ext {
    // Where you will see your artifact in Bintray's web interface
    // The "bintrayName" should match the name of the Bintray repo.
    bintrayRepo = 'maven'
    bintrayName = 'GalleryModule'

    // Maven metadata
    publishedGroupId = 'es.guiguegon'
    libraryName = 'GalleryModule'
    // Save yourself a head ache, and set this equal to the name of the Android Studio library
    // module. The artifact name needs to match the name of the library.
    artifact = 'gallerymodule'

    libraryDescription =
            'Just a simple activity that show all the images and videos in a gallery and allows to select one and retrieve it'
    libraryVersion = versionLib

    developerId = 'guiguegon'
    developerName = 'Guillermo Guerrero Gonz√°lez'
    developerEmail = 'guiguegon@gmail.com'
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "GalleryModule"
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = versionLib
            desc = 'Just a simple activity that show all the images and videos in a gallery and allows to select one and retrieve it'
            vcsUrl = 'https://github.com/guiguegon/gallerymodule.git'
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName versionLib
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '24.1.1'

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    //Dexter (permissions)
    compile 'com.karumi:dexter:2.2.2'

    //Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    //RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/borjabravo10/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/borjabravo10/JCenter/master/bintrayv1.gradle'
}

bintrayUpload.mustRunAfter install
task remoteUpload(dependsOn: ['install', 'bintrayUpload'])